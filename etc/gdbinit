set env LD_LIBRARY_PATH ../lib:../../lib
set env DYLD_LIBRARY_PATH ../lib:../../lib
set print elements 64
define dtype
  printf "%s\n",fd_dtype2string($arg0)
end
define showtype
  printf "object type is 0x%x: %s\n",_fd_ptr_type($arg0),fd_type_names[_fd_ptr_type($arg0)]
end
define refcount
  if (($arg0)&3)
  print "not a cons" 
  else
  printf "object refcount=%d\n",((((struct FD_CONS *)$arg0)->consbits)>>7)
  end
end
define showstruct 
  print *((struct $arg1 *)($arg0))
end
define showfield
  print ((struct $arg1 *)($arg0))->$arg2
end
define showstring
  set print elements 0
  print ((struct FD_STRING *)($arg0))->bytes
  set print elements 16
end
define showsymbol
  printf "%s\n",((struct FD_STRING *)(fd_symbol_names[((($arg0)>>2)&0x7FFFFF)]))->bytes
end
define showoid
  print/x fd_base_oids[(($arg0>>2)&0x3FF)]+($arg0>>12)
end
define showcar
  showtype((((struct FD_PAIR *)$arg0)->car))
  print/x (((struct FD_PAIR *)$arg0)->car)
end
define showcdr
  showtype (((struct FD_PAIR *)$arg0)->cdr)
  print/x (((struct FD_PAIR *)$arg0)->cdr)
end
define showvelt
   print/x ((((struct FD_VECTOR *)$arg0)->data)[$arg1])
   showtype $
end
define showcelt
  print/x ((&((struct FD_CHOICE *)$arg0)->elt0)[$arg1])
  showtype $  
end
define oidinfo
  printf "oidinfo: %s\n", (_fd_oid_info($arg0))
end
define showchoice
  print ((((struct FD_CHOICE *)$arg0)->size)&(0x7FFFFFFF))
end
define findelt
  print _fd_find_elt($arg0,$arg1,$arg2)
end
define inchoicep
  print _fd_find_elt($arg0,&(((struct FD_CHOICE *)$arg1)->elt0),((((struct FD_CHOICE *)$arg1)->size)&(0x7FFFFFFF)))
end

set breakpoint pending on

break u8_push_exception
break _fd_debug
break u8_raise
break abort
#break exit

# For LIBC
set env MALLOC_CHECK_=2
# For OS X
#set env MallocHelp=1 
set env MallocGuardEdges=1
set env MallocScribble=1
set env MallocBadFreeAbort=1
set env MallocCheckHeapAbort=1

define check_heap
  set env MallocCheckHeapStart=0
  set env MallocCheckHeapEach=$arg0
end

# Init fdb print limits (init done through the config interface)
set env FD_DISPLAYMAXELTS=17
set env FD_DISPLAYMAXCHARS=256

